---
page_title: "azapi_resource_action Action - terraform-provider-azapi"
subcategory: ""
description: |-
  Perform an action on an existing Azure resource (stateless)
---

# azapi_resource_action (Action)

Perform an action on an existing Azure resource (stateless)

~> **Note:** The `azapi_resource_action` Action is in beta. Its interface and behaviour may change as the feature evolves, and breaking changes are possible. It is offered as a technical preview without compatibility guarantees until Terraform 1.14 is generally available.

## Example Usage

```terraform
terraform {
  required_providers {
    azurerm = {
      source = "hashicorp/azurerm"
    }
    azapi = {
      source = "Azure/azapi"
    }
  }
}

provider "azurerm" {
  features {}
}

provider "azapi" {}

variable "admin_password" {
  type      = string
  sensitive = true
}

resource "azurerm_resource_group" "example" {
  name     = "example-resources"
  location = "West Europe"
}

resource "azurerm_virtual_network" "example" {
  name                = "example-network"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.example.location
  resource_group_name = azurerm_resource_group.example.name
}

resource "azurerm_subnet" "example" {
  name                 = "internal"
  resource_group_name  = azurerm_resource_group.example.name
  virtual_network_name = azurerm_virtual_network.example.name
  address_prefixes     = ["10.0.2.0/24"]
}

resource "azurerm_network_interface" "example" {
  name                = "example-nic"
  location            = azurerm_resource_group.example.location
  resource_group_name = azurerm_resource_group.example.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.example.id
    private_ip_address_allocation = "Dynamic"
  }
}

resource "azurerm_windows_virtual_machine" "example" {
  name                = "example-vm"
  resource_group_name = azurerm_resource_group.example.name
  location            = azurerm_resource_group.example.location
  size                = "Standard_F2"
  admin_username      = "adminuser"
  admin_password      = var.admin_password
  network_interface_ids = [
    azurerm_network_interface.example.id,
  ]

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "MicrosoftWindowsServer"
    offer     = "WindowsServer"
    sku       = "2016-Datacenter"
    version   = "latest"
  }

  tags = {
    environment = "production"
  }

  lifecycle {
    action_trigger {
      events  = [before_update]
      actions = [action.azapi_resource_action.power_off]
    }

    action_trigger {
      events  = [after_update]
      actions = [action.azapi_resource_action.power_on]
    }
  }
}

action "azapi_resource_action" "power_off" {
  config {
    type        = "Microsoft.Compute/virtualMachines@2023-03-01"
    resource_id = azurerm_windows_virtual_machine.example.id
    action      = "powerOff"
  }
}

action "azapi_resource_action" "power_on" {
  config {
    type        = "Microsoft.Compute/virtualMachines@2023-03-01"
    resource_id = azurerm_windows_virtual_machine.example.id
    action      = "start"
  }
}
```

<!-- action schema generated by tfplugindocs -->
## Schema

### Required

- `resource_id` (String) The ID of an existing Azure resource.
- `type` (String) In a format like `<resource-type>@<api-version>`. `<resource-type>` is the Azure resource type, for example, `Microsoft.Storage/storageAccounts`. `<api-version>` is version of the API used to manage this azure resource.

### Optional

- `action` (String) The name of the resource action. It's also possible to make HTTP requests towards the resource ID if leave this field empty.
- `body` (Dynamic) A dynamic attribute that contains the request body.
- `headers` (Map of String) Headers to include in the request.
- `locks` (List of String) A list of ARM resource IDs which are used to avoid create/modify/delete azapi resources at the same time.
- `method` (String) HTTP method for the action. Defaults to POST.
- `query_parameters` (Map of List of String) Query parameters to include in the request.
