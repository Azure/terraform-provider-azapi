---
layout: "azapi"
page_title: "AzAPI Provider: 2.0 Upgrade Guide"
description: |-
  This guide will cover how to upgrade from version 1.x to version 2.0 of the AzAPI Provider.

---

# Azapi Provider

## Azapi Provider Version v2.0

### Considerations

Version 2.0 of the Azapi Provider is a major release and includes breaking changes which are outlined in this document.

When upgrading to version 2.0 of the Azure Provider, we recommend upgrading to the latest version of Terraform Core ([which can be found here](https://www.terraform.io/downloads)).

This guide will continue to receive updates as we incorporate feedback and continue to fix any issues during the Beta.

### Pinning your Provider Version

We recommend pinning the version of each Provider you use in Terraform - you can do this using the `version` attribute within the `required_providers` block, either to a specific version of the Azapi Provider, like so:

```hcl
terraform {
  required_providers {
    azapi = {
      source  = "azure/azapi"
      version = "=1.15.0"
    }
  }
}

provider "azapi" {
}
```

.. or to any 1.0 release:

```hcl
terraform {
  required_providers {
    azapi = {
      source  = "azure/azapi"
      version = "~> 1.0"
    }
  }
}

provider "azapi" {
}
```

More information on [how to pin the version of a Terraform Provider being used can be found on the Terraform Website](https://www.terraform.io/language/providers/requirements#requiring-providers).

---


## What's available in Version 2.0 of the Azapi Provider?

Below is an overview of the changes coming 2.0. Each topic is covered in more detail further down.

* [Dynamic Properties Support](#dynamic-properties-support)
* [Provider Functions](#provider-functions)
* [Cusomtized Retriable Configuration](#customized-retriable-configuration)
* [Headers and Query Parameters Support](#headers-and-query-parameters-support)
* [Replacement Triggers](#replacement-triggers)
* [JMESPath Query Support](#jmespath-query-support)
* [Preflight Validation](#preflight-validation)
* [Breaking Changes](#breaking-changes)


## Dynamic Properties Support

Previously, AzAPI resource definitions required JSON encoding and decoding to handle the requst and response payloads. This was not perfect because plan output was not clear when sensivite data was involved, and Terraform native lifecycle management was not supported.

The dynamic properties support makes it much easier to define resources and data sources in the AzAPI Provider. This feature is available for all resources and data sources in the AzAPI Provider.

```hcl
resource "azapi_resource" "automationAccount" {
  type      = "Microsoft.Automation/automationAccounts@2023-11-01"
  parent_id = azapi_resource.resourceGroup.id
  name      = "example-automation-account"
  location  = "westeurope"

  // it's not necessary to use `jsonencode` function to encode the HCL object to JSON, just use the HCL object directly
  body = {
    properties = {
      encryption = {
        keySource = "Microsoft.Automation"
      }
      publicNetworkAccess = true
      sku = {
        name = "Basic"
      }
    }
  }
  response_export_values = ["properties"]

  // native lifecycle management
  lifecycle {
    ignore_changes = [
      body.properties.encryption.keySource
    ]
  }
}

output "o1" {
  // it's not necessary to use `jsondecode` function to decode the response
  value = azapi_resource.automationAccount.output.properties.automationHybridServiceUrl
}
```

For more information, refer to [this blogpost](https://techcommunity.microsoft.com/t5/azure-tools-blog/announcing-azapi-dynamic-properties/ba-p/4121855)

## Provider Functions

With the addition of Provider Functions in Terraform 1.8, we've added a number of functions to the AzAPI Provider to make it easier to work with the Azure Resource IDs.

- **New Provider Function**: build_resource_id
- **New Provider Function**: parse_resource_id
- **New Provider Function**: subscription_resource_id
- **New Provider Function**: tenant_resource_id
- **New Provider Function**: management_group_resource_id
- **New Provider Function**: resource_group_resource_id
- **New Provider Function**: extension_resource_id

`build_resource_id` - This function constructs an Azure resource ID given the parent ID, resource type, and resource name. It is useful for creating resource IDs for top-level and nested resources within a specific scope.

```hcl
// it will output "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/myResourceGroup/providers/Microsoft.Network/virtualNetworks/myVNet"
output "resource_id" {
  value = provider::azapi::build_resource_id("/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/myResourceGroup", "Microsoft.Network/virtualNetworks", "myVNet")
}
```

`parse_resource_id` - This function takes an Azure resource ID and a resource type and parses the ID into its individual components such as subscription ID, resource group name, provider namespace, and other parts.

```hcl
# it will output below object
# {
#   "id" = "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/myResourceGroup/providers/Microsoft.Network/virtualNetworks/myVNet"
#   "name" = "myVNet"
#   "parent_id" = "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/myResourceGroup"
#   "parts" = tomap({
#     "providers" = "Microsoft.Network"
#     "resourceGroups" = "myResourceGroup"
#     "subscriptions" = "00000000-0000-0000-0000-000000000000"
#     "virtualNetworks" = "myVNet"
#   })
#   "provider_namespace" = "Microsoft.Network"
#   "resource_group_name" = "myResourceGroup"
#   "subscription_id" = "00000000-0000-0000-0000-000000000000"
#   "type" = "Microsoft.Network/virtualNetworks"
# }
output "parsed_resource_id" {
  value = provider::azapi::parse_resource_id("Microsoft.Network/virtualNetworks", "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/myResourceGroup/providers/Microsoft.Network/virtualNetworks/myVNet")
}
```

`subscription_resource_id` - This function constructs an Azure subscription scope resource ID given the subscription ID, resource type, and resource names.

```hcl
// it will output "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/rg1"
output "subscription_resource_id" {
  value = provider::azapi::subscription_resource_id("00000000-0000-0000-0000-000000000000", "Microsoft.Resources/resourceGroups", ["rg1"])
}
```

`tenant_resource_id` - This function constructs an Azure tenant scope resource ID given the resource type and resource names.

```hcl
// it will output "/providers/Microsoft.Billing/billingAccounts/ba1/billingProfiles/bp1"
output "tenant_resource_id" {
  value = provider::azapi::tenant_resource_id("Microsoft.Billing/billingAccounts/billingProfiles", ["ba1", "bp1"])
}
```

`management_group_resource_id` - This function constructs an Azure management group scope resource ID given the management group name, resource type, and resource names.

```hcl
// it will output "/providers/Microsoft.Management/managementGroups/mg1/providers/Microsoft.Billing/billingAccounts/ba1/billingProfiles/bp1"
output "management_group_resource_id" {
  value = provider::azapi::management_group_resource_id("mg1", "Microsoft.Billing/billingAccounts/billingProfiles", ["ba1", "bp1"])
}
```

`resource_group_resource_id` - This function constructs an Azure resource group scope resource ID given the subscription ID, resource group name, resource type, and resource names.

```hcl
// it will output "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/rg1/providers/Microsoft.Network/virtualNetworks/vnet1/subnets/subnet1"
output "resource_group_resource_id" {
  value = provider::azapi::resource_group_resource_id("00000000-0000-0000-0000-000000000000", "rg1", "Microsoft.Network/virtualNetworks/subnets", ["vnet1", "subnet1"])
}
```

`extension_resource_id` - This function constructs an Azure extension resource ID given the base resource ID, resource type, and additional resource names.


```hcl
// it will output "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/rg1/providers/Microsoft.Network/virtualNetworks/vnet1/providers/Microsoft.Authorization/locks/mylock"
output "extension_resource_id" {
  value = provider::azapi::extension_resource_id("/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/rg1/providers/Microsoft.Network/virtualNetworks/vnet1", "Microsoft.Authorization/locks", ["mylock"])
}
```

## Customized Retriable Configuration

The AzAPI Provider now supports customized retriable configuration for Azure API requests. You can configure regular expression to match the error message and the retry policy.

```hcl

data "azapi_resource" "test" {
  type = "Microsoft.Resources/resourceGroups@2024-03-01"
  name = "example"

  retry = {
    error_message_regex  = ["ResourceGroupNotFound"]
    interval_seconds     = 5
    max_interval_seconds = 30
    multiplier           = 1.5
    randomization_factor = 0.5
  }
}

```

## Headers and Query Parameters Support

The AzAPI Provider now supports headers and query parameters in the request. You can configure headers and query parameters in the resource and data source definitions.

```hcl

data "azapi_client_config" "current" {}

data "azapi_resource_list" "test" {
  type      = "Microsoft.Authorization/policyDefinitions@2021-06-01"
  parent_id = "/subscriptions/${data.azapi_client_config.current.subscription_id}"
  query_parameters = {
    "$filter" = ["policyType eq 'BuiltIn'"]
  }
  response_export_values = ["*"]
}
```

It's also possible to configure headers and query parameters for CRUD operations.

```hcl

resource "azapi_resource" "example" {
  type     = "Microsoft.Resources/resourceGroups@2021-04-01"
  name     = "example"
  location = "westus"

  create_query_parameters = {
    "query1" = ["create-value"]
  }
  update_query_parameters = {
    "query1" = ["update-value"]
  }
  delete_query_parameters = {
    "query1" = ["delete-value"]
  }
  read_query_parameters = {
    "query1" = ["read-value"]
  }

  create_headers = {
    "header1" = "create-value"
  }
  update_headers = {
    "header2" = "update-value"
  }
  delete_headers = {
    "header3" = "delete-value"
  }
  read_headers = {
    "header4" = "read-value"
  }
}

```

## Replacement Triggers

Previously, it was difficult to replace a resource when the changes could not be updated in place. The AzAPI Provider now supports replacement triggers to help you manage these situations. And there are two types of replacement triggers: value based and reference based.

1. **Value Based Replacement Triggers**: `replace_triggers_external_values` - Will trigger a replace of the resource when the value changes and is not `null`.

```hcl
// e.g. to replace a resource when either the SKU or os_type attributes change:
resource "azapi_resource" "example" {
  name      = var.name
  type      = "Microsoft.Network/publicIPAddresses@2023-11-01"
  parent_id = "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/example"
  body = {
    properties = {
      sku   = var.sku
      zones = var.zones
    }
  }

  replace_triggers_external_values = [
    var.sku,
    var.zones,
  ]
}
```

2. **Reference Based Replacement Triggers**: `replace_triggers_refs` - A list of paths in the current Terraform configuration. When the values at these paths change, the resource will be replaced.

```hcl
// e.g. to replace a resource when either the SKU or os_type attributes change:
resource "azapi_resource" "example" {
  name      = var.name
  type      = "Microsoft.Network/publicIPAddresses@2023-11-01"
  parent_id = "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/example"
  body = {
    properties = {
      sku   = var.sku
      zones = var.zones
    }
  }

  replace_triggers_refs = [
    "properties.sku",
    "properties.zones",
  ]
}
```


## JMESPath Query Support

The AzAPI Provider now supports JMESPath queries for filtering and transforming the response data. You can use JMESPath queries in the `response_export_values` attribute to filter and transform the response data.

The `response_export_values` attribute accepts a map where the key is the name for the result and the value is a JMESPath query string to filter the response. 

```hcl

data "azapi_resource" "example" {
  type      = "Microsoft.ContainerRegistry/registries@2020-11-01-preview"
  parent_id = "/subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/example"
  name      = "example"
  response_export_values = {
    login_server      = "properties.loginServer"
    quarantine_status = "properties.policies.quarantinePolicy.status"
  }
}

// it will output below object
// {
//   "login_server" = "example.azurecr.io"
//   "quarantine_status" = "Enabled" 
// }

output "login_server" {
  // it will output "example.azurecr.io"
  value = data.azapi_resource.example.output.login_server
}

output "quarantine_status" {
  // it will output "Enabled"
  value = data.azapi_resource.example.output.quarantine_status
}

```

## Preflight Validation

Preflight validation is a new feature in the AzAPI Provider that helps ensure your configuration will deploy successfully. To enable preflight validation, set the `enable_preflight` attribute to `true` in the provider block.

In the example below, the provider block is configured to enable preflight validation. The `azapi_resource` block defines a virtual network resource with an invalid address prefix. When you run `terraform plan`, the preflight validation will fail and display an error message.

```hcl
provider "azapi" {
  enable_preflight = true
}

resource "azapi_resource" "vnet" {
  type      = "Microsoft.Network/virtualNetworks@2024-01-01"
  parent_id = azapi_resource.resourceGroup.id
  name      = "example-vnet"
  location  = "westus"
  body = {
    properties = {
      addressSpace = {
        addressPrefixes = [
          "10.0.0.0/160",
        ]
      }
    }
  }
}
```

When you run `terraform plan`, you will see the following error message:

```shell
{
  "code": "InvalidAddressPrefixFormat",
  "target": "/subscriptions/....../resourceGroups/olgdwamq/providers/Microsoft.Network/virtualNetworks/example-vnet",
  "message": "Address prefix 10.0.0.0/160 of resource /subscriptions/....../resourceGroups/olgdwamq/providers/Microsoft.Network/virtualNetworks/example-vnet is not formatted correctly. It should follow CIDR notation, for example 10.0.0.0/24.",
  "details": []
}
```


## Breaking Changes

- Provider field `default_naming_prefix` and `default_naming_suffix` are removed. 

  How to fix:  
  Please remove the `default_naming_prefix` and `default_naming_suffix` fields from the provider block. You can specify the naming prefix and suffix in the resource's `name` field instead.

- Provider field `enable_hcl_output_for_data_source` is removed. The `output` field in the data source is always in HCL format.

  How to fix:  
  Please remove the `enable_hcl_output_for_data_source` field from the provider block. Remove any `jsondecode` functions when using the `output` field in the data source.

- The `azapi_resource`'s `removing_special_chars` field is removed. Please specify the `name` field and remove the special characters in the `name` field instead.

  How to fix:  
  Please remove the `removing_special_chars` field from the `azapi_resource` block and any special characters in the `name` field.

- The `ignore_body_changes` field is removed. 

  How to fix:  
  Please remove the `ignore_body_changes` field from the `azapi_resource` block. And use the `lifecycle.ignore_changes` to ignore some properties when comparing the resource with its current state.

  ```hcl
  resource "azapi_resource" "virtualNetwork" {
    type      = "Microsoft.Network/virtualNetworks@2022-07-01"
    parent_id = azapi_resource.resourceGroup.id
    name      = var.resource_name
    location  = var.location
    body = {
      properties = {
        addressSpace = {
          addressPrefixes = [
            "10.0.0.0/16",
          ]
        }
      }
    }
    // the deprecated field
    // ignore_body_changes = ["properties.subnets"]

    // use the lifecycle block instead
    lifecycle {
      ignore_changes = [body.properties.subnets]
    }
  }
  ```

- The `body` field now only accepts an HCL object.

  How to fix:
  Please remove the `jsonencode` function when using the `body` field in the resource block.

  ```hcl
  resource "azapi_resource" "subnet" {
    type      = "Microsoft.Network/virtualNetworks/subnets@2022-07-01"
    parent_id = azapi_resource.virtualNetwork.id
    name      = var.resource_name
    // it's not necessary to use `jsonencode` function to encode the HCL object to JSON, just use the HCL object directly
    // body = jsonencode({
    //  properties = {
    //   addressPrefix = "10.0.2.0/24"
    //  }
    // })
    // use the HCL object directly
    body = {
      properties = {
        addressPrefix = "10.0.2.0/24"
      }
    }
  }
  ```

- The `output` field now only exports an HCL object.
  
    How to fix:  
    Please remove the `jsondecode` function when using the `output` field in the resource block.
  
    ```hcl
    output "login_server" {
      // it's not necessary to use `jsondecode` function to decode the response
      // value = jsondecode(azapi_resource.example.output).properties.loginServer
      // use the HCL object directly
      value = azapi_resource.example.output.properties.loginServer
    }
    ```

- The `use_msi` field now defaults to `false`.

  How to fix:  
  Please set it to `true` explicitly if you want to authenticate using Managed Service Identity.

- `azapi_resource`, `azapi_update_resource` resources and data sources' `output` field defaults to the readonly fields when the `response_export_values` is not specified.  
  When run the `terraform plan` command, the output will show the computed field `output` has changed.

  How to fix:
  1. Run `terraform refresh` to update the state file.
  2. Specify the `disable_default_output = true` in the provider block to disable the default output.

- `azapi_resource_list` data source's `output` field defaults to the response when the `response_export_values` is not specified.

  How to fix:
  1. Run `terraform refresh` to update the state file.
  2. Specify the `disable_default_output = true` in the provider block to disable the default output.
